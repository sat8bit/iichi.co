buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
    }
}

subprojects {
    group 'iichi.co'

    apply plugin: 'java'
    apply plugin: 'war'

    repositories {
        mavenCentral()
    }

    dependencies {
        providedCompile('org.projectlombok:lombok:1.16.6')
        testCompile('junit:junit:4.11')
        testCompile('org.mockito:mockito-all:1.8.4')
    }
}

project(":web") {
    version '0.0.1'
    apply plugin: "spring-boot"

    dependencies {
        // wildflyにdeployするために不要なtomcatの排除
        compile('org.springframework.boot:spring-boot-starter-web:1.3.2.RELEASE') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        // spring-bootはhibernate 4に依存しているので5で上書き
        compile('org.hibernate:hibernate-core:5.0.7.Final')
        compile('org.hibernate:hibernate-entitymanager:5.0.7.Final')

        // コンパイル用
        providedCompile('javax.servlet:javax.servlet-api:3.1.0')

        // bootRun用
        providedRuntime('org.springframework.boot:spring-boot-starter-tomcat:1.3.2.RELEASE')

        // 以下普通に依存関係
        compile('org.thymeleaf:thymeleaf:2.1.4.RELEASE')
        compile('org.thymeleaf:thymeleaf-spring4:2.1.4.RELEASE')
        compile('nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.3.1')
        compile project(":domain")
        compile project(":common")
    }
}

project(":domain") {
    dependencies {
        compile('org.hibernate:hibernate-entitymanager:5.0.7.Final')
        compile('mysql:mysql-connector-java:5.1.38')
        compile project(":common")
    }
}

project(":common") {
    dependencies {
        compile('com.fasterxml.jackson.core:jackson-core:2.7.0')
        compile('org.springframework.boot:spring-boot-starter-web:1.3.2.RELEASE') {
            exclude module: 'spring-boot-starter-tomcat'
        }
        providedCompile('javax.servlet:javax.servlet-api:3.1.0')
    }
}